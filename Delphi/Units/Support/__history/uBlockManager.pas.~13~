unit uBlockManager;

interface
uses
  uBase, uCommands, uAutoClearStack, Vcl.ExtCtrls, uSwitchStatements,
  Winapi.Windows, uAdditionalTypes, uDrawShapes, Vcl.Graphics;
type

  TBlockManager = class
  public type
    TSetScrollPosProc = procedure(const AStatement: TStatement) of object;
  private const
    SchemeInitialIndent = 10;
  private
    FMainBlock : TBlock;
    FDedicatedStatement: TStatement;
    FCarryBlock: TBlock;
    FBufferBlock: TBlock;
    FUndoStack, FRedoStack: TAutoClearStack<ICommand>;
    FPaintBox: TPaintBox;
    FHighlightColor: TColor;

    procedure ChangeDedicated(const AStatement: TStatement);
  public
    constructor Create(const APaintBox: TPaintBox; const AHighlightColor: TColor);
    destructor Destroy;
    property MainBlock: TBlock read FMainBlock;
    property CarryBlock: TBlock read FCarryBlock;
    property BufferBlock: TBlock read FBufferBlock;
    property UndoStack: TAutoClearStack<ICommand> read FUndoStack;
    property RedoStack: TAutoClearStack<ICommand> read FRedoStack;
    property HighlightColor: TColor write FHighlightColor;
    property DedicatedStatement: TStatement read FDedicatedStatement write ChangeDedicated;

    procedure TryCutDedicated;
    procedure TryCopyDedicated;
    procedure TryDeleteDedicated;
    procedure TryMoveDedicated(const ASetScrollPosProc: TSetScrollPosProc; const AKey: Integer);

    procedure TryInsertBufferBlock;

    procedure CreateCarryBlock;
    procedure MoveCarryBlock(const ADeltaX, ADeltaY: Integer);
    procedure DestroyCarryBlock;

    procedure Draw(const AVisibleImageRect: TVisibleImageRect);
  end;

implementation

  destructor TBlockManager.Destroy;
  begin
    FMainBlock.Destroy;
    FBufferBlock.Destroy;

    FUndoStack.Destroy;
    FRedoStack.Destroy;

    inherited;
  end;

  constructor TBlockManager.Create(const APaintBox: TPaintBox; const AHighlightColor: TColor);
  begin
    FPaintBox:= APaintBox;
    FHighlightColor:= AHighlightColor;

    FUndoStack := TAutoClearStack<ICommand>.Create;
    FRedoStack := TAutoClearStack<ICommand>.Create;

    FDedicatedStatement:= nil;
    FCarryBlock:= nil;

    FBufferBlock:= TBlock.Create(0, FPaintBox.Canvas);
    FBufferBlock.AddStatement(uBase.DefaultStatement.CreateUncertainty(FBufferBlock));

    FMainBlock:= TBlock.Create(SchemeInitialIndent, FPaintBox.Canvas);
    FMainBlock.AddUnknownStatement(uBase.DefaultStatement.CreateUncertainty(FMainBlock),
                                                            SchemeInitialIndent);
  end;

  procedure TBlockManager.TryCutDedicated;
  begin
    TryCopyDedicated;
    TryDeleteDedicated;
  end;

  procedure TBlockManager.TryCopyDedicated;
  begin
    if FDedicatedStatement <> nil then
    begin
      FBufferBlock.Destroy;

      FBufferBlock := TBlock.Create(nil);
      FBufferBlock.Assign(FDedicatedStatement.BaseBlock);

      FBufferBlock.AddStatement(FDedicatedStatement.Clone);
    end;
  end;

  procedure TBlockManager.TryDeleteDedicated;
  begin
    if FDedicatedStatement <> nil then
    begin
      FRedoStack.Clear;
      FUndoStack.Push(TCommandDelStatement.Create(FDedicatedStatement));
      FUndoStack.Peek.Execute;

      FDedicatedStatement:= nil;
      FPaintBox.Invalidate;
    end;
  end;

  procedure TBlockManager.TryMoveDedicated(const ASetScrollPosProc: TSetScrollPosProc; const AKey: Integer);
  begin
    case AKey of
      VK_LEFT:
      begin
        SetHorizontalMovement(FDedicatedStatement, FMainBlock, uSwitchStatements.BackwardDir);
        ASetScrollPosProc(FDedicatedStatement);
        FPaintBox.Invalidate;
      end;
      VK_RIGHT:
      begin
        SetHorizontalMovement(FDedicatedStatement, FMainBlock, uSwitchStatements.ForwardDir);
        ASetScrollPosProc(FDedicatedStatement);
        FPaintBox.Invalidate;
      end;
      VK_UP:
      begin
        SetVerticalMovement(FDedicatedStatement, FMainBlock, uSwitchStatements.BackwardDir);
        ASetScrollPosProc(FDedicatedStatement);
        FPaintBox.Invalidate;
      end;
      VK_DOWN:
      begin
        SetVerticalMovement(FDedicatedStatement, FMainBlock, uSwitchStatements.ForwardDir);
        ASetScrollPosProc(FDedicatedStatement);
        FPaintBox.Invalidate;
      end;
    end;
  end;

  procedure TBlockManager.TryInsertBufferBlock;
  var
    BaseBlock: TBlock;
  begin
    if (FBufferBlock.Statements.Count <> 0) and (FDedicatedStatement <> nil) then
    begin
      BaseBlock:= FDedicatedStatement.BaseBlock;

      FRedoStack.Clear;
      FUndoStack.Push(TCommandAddBlock.Create(BaseBlock,
                      BaseBlock.FindStatementIndex(FDedicatedStatement.YStart) + 1,
                      FBufferBlock));
      FUndoStack.Peek.Execute;

      FDedicatedStatement:= FBufferBlock.Statements.GetLast;

      FBufferBlock := TBlock.Create(nil);
      FBufferBlock.Assign(FDedicatedStatement.BaseBlock);
      FBufferBlock.AddStatement(FDedicatedStatement.Clone);

      FPaintBox.Invalidate;
    end;
  end;

  procedure TBlockManager.Draw(const AVisibleImageRect: TVisibleImageRect);
  const
    Stock = 420;
  begin
    AVisibleImageRect.Expand(Stock);
    FPaintBox.Width := FMainBlock.XLast + Stock;
    FPaintBox.Height := FMainBlock.Statements.GetLast.GetYBottom + Stock;

    if FDedicatedStatement <> nil then
      ColorizeRect(FPaintBox.Canvas, FDedicatedStatement.BaseBlock.XStart,
                   FDedicatedStatement.BaseBlock.XLast,
                   FDedicatedStatement.YStart,
                   FDedicatedStatement.GetYBottom,
                   FHighlightColor);

    if FCarryBlock <> nil then
      FCarryBlock.DrawBlock(AVisibleImageRect);

    FMainBlock.DrawBlock(AVisibleImageRect);
    //DrawCoordinates(FPaintBox.Canvas, 50);
  end;

  procedure TBlockManager.ChangeDedicated(const AStatement: TStatement);
  begin
    FDedicatedStatement:= AStatement;
    FPaintBox.Invalidate;
  end;

  procedure TBlockManager.CreateCarryBlock;
  begin
    FCarryBlock:= TBlock.Create(nil);
    FCarryBlock.Assign(FDedicatedStatement.BaseBlock);

    FCarryBlock.AddStatement(FDedicatedStatement.Clone);
  end;

  procedure TBlockManager.MoveCarryBlock(const ADeltaX, ADeltaY: Integer);
  begin
    FCarryBlock.MoveRight(ADeltaX);
    FCarryBlock.MoveDown(ADeltaY);
  end;

  procedure TBlockManager.DestroyCarryBlock;
  begin
    FCarryBlock.Destroy;
    FCarryBlock:= nil;

    FPaintBox.Invalidate;
  end;

end.
