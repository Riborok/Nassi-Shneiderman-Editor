unit List;

interface
uses System.SysUtils, System.Generics.Collections,  System.Generics.Defaults;

type
  TArrayList<T> = class(TEnumerable<T>)
  private
    FArray: array of T;
    FCount: Integer;
    FCapacity: Integer;
    procedure Grow;
    function GetItem(Index: Integer): T;
    procedure SetItem(Index: Integer; Value: T);
  protected
    function DoGetEnumerator: TEnumerator<T>; override;
  public
    constructor Create(InitialCapacity: Integer = 0);
    destructor Destroy; override;
    procedure Add(Item: T);
    procedure Insert(Index: Integer; Item: T);
    procedure Clear;
    property Items[Index: Integer]: T read GetItem write SetItem; default;
    property Count: Integer read FCount;

    type
      TEnumerator = class(TEnumerator<T>)
      private
        FList: TArrayList<T>;
        FIndex: Integer;
        function GetCurrent: T; inline;
      protected
        function DoGetCurrent: T; override;
        function DoMoveNext: Boolean; override;
      public
        constructor Create(const AList: TArrayList<T>);
        function MoveNext: Boolean; inline;
        property Current: T read GetCurrent;
      end;

    function GetEnumerator: TEnumerator; reintroduce; inline;
  end;

implementation

  constructor TArrayList<T>.Create(InitialCapacity: Integer = 0);
  begin
    FCount := 0;
    FCapacity := InitialCapacity;
    SetLength(FArray, FCapacity);
  end;

  destructor TArrayList<T>.Destroy;
  begin
    Clear;

    inherited;
  end;

  procedure TArrayList<T>.Add(Item: T);
  begin

    if FCount = FCapacity then
      Grow;

    FArray[FCount] := Item;
    Inc(FCount);
  end;

  procedure TArrayList<T>.Insert(Index: Integer; Item: T);
  var
    I: Integer;
  begin

    if (Index < 0) or (Index > FCount) then
      raise Exception.Create('Index out of bounds');

    if FCount = FCapacity then
      Grow;

    for I := FCount - 1 downto Index do
      FArray[I + 1] := FArray[I];

    FArray[Index] := Item;
    Inc(FCount);
  end;

  procedure TArrayList<T>.Clear;
  begin

    SetLength(FArray, 0);

    FCount := 0;

    FCapacity := 0;
  end;

  function TArrayList<T>.GetItem(Index: Integer): T;
  begin

    if (Index < 0) or (Index >= FCount) then
      raise Exception.Create('Index out of bounds');

    Result := FArray[Index];
  end;

  procedure TArrayList<T>.SetItem(Index: Integer; Value: T);
  begin

    if (Index < 0) or (Index >= FCount) then
      raise Exception.Create('Index out of bounds');

    FArray[Index] := Value;
  end;

  procedure TArrayList<T>.Grow;
  begin
    FCapacity := FCapacity * 2 + 4;
    SetLength(FArray, FCapacity);
  end;


  { Enumerator }

  function TArrayList<T>.DoGetEnumerator: TEnumerator<T>;
  begin
    Result := TEnumerator.Create(Self);
  end;

  constructor TArrayList<T>.TEnumerator.Create(const AList: TArrayList<T>);
  begin
    inherited Create;
    FList := AList;
    FIndex := -1;
  end;

  function TArrayList<T>.TEnumerator.GetCurrent: T;
  begin
    Result := FList[FIndex];
  end;

  function TArrayList<T>.TEnumerator.DoGetCurrent: T;
  begin
    Result := Current;
  end;

  function TArrayList<T>.TEnumerator.DoMoveNext: Boolean;
  begin
    Result := MoveNext;
  end;

  function TArrayList<T>.GetEnumerator: TEnumerator;
  begin
    Result := TEnumerator.Create(Self);
  end;

  function TArrayList<T>.TEnumerator.MoveNext: Boolean;
  begin
    Inc(FIndex);
    Result := FIndex < FList.Count;
  end;

end.

